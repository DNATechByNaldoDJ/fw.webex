/*
  __                            _
 / _|__      ____      __  ___ | |__    ___ __  __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\

Released to Public Domain.

loocal oNav:=WebExNavTop():New("FWWebEx")
oNav:AddLink("Home","#home",WebExIcon():New("bi-house"))
oNav:AddDropDown("Acoes", {;
    ,{"Salvar","#salvar"};
    ,{"Excluir","#excluir"};
    ,{"Sair","#sair"};
})

--------------------------------------------------------------------------------------
*/

namespace FWWebEx

#include "fw.webex.th"

class WebExNavTop from WebExControl

    public data cBrand as character

    public data jLinks as json
    public data jDropDowns as json

    public method New(cBrand as character) as object
    public method AddLink(cLabel as character,cHRef as character,oIcon as object) as object
    public method AddDropDown(cLabel as character,aItems as array) as object
    public method RenderHTML() as character

endclass

method New(cBrand) class WebExNavTop
    paramtype 1 var cBrand as character optional default ""
    _Super:New("nav")
    ::AddClass("navbar mb-4")
    ::cBrand:=cBrand
    ::jLinks:=JSONObject():New()
    ::jDropDowns:=JSONObject():New()
return(self)

method AddLink(cLabel,cHRef,oIcon) class WebExNavTop
    local oLink as object
    paramtype 1 var cLabel as character
    paramtype 2 var cHRef as character
    paramtype 3 var oIcon as object class WEBEXICON optional
    oLink:=WebExControl():New("a")
    oLink:SetAttr("class","nav-link")
    oLink:SetAttr("href",cHRef)
    if (oIcon!=nil)
        oLink:AddChild(oIcon)
        oLink:AddContent(" ")
    endif
    oLink:AddContent(cLabel)
    ::jLinks[oLink:cID]:=oLink
return(oLink)

method AddDropDown(cLabel,aItems) class WebExNavTop

    local i as numeric

    local oItem as object
    local oLink as object
    local oMenu as object
    local oWrapper as object
    local oToggle as object

    oToggle := WebExControl():New("a")
    oToggle:SetAttr("class","nav-link dropdown-toggle")
    oToggle:SetAttr("href","#")
    oToggle:SetAttr("role","button")
    oToggle:SetAttr("data-bs-toggle","dropdown")
    oToggle:SetAttr("aria-expanded","false")
    oToggle:SetContent(cLabel)

    oMenu:=WebExControl():New("ul")
    oMenu:SetAttr("class","dropdown-menu")
    for i:=1 to Len(aItems)
        oItem:=WebExControl():New("li")
        oLink:=WebExControl():New("a")
        oLink:SetAttr("class","dropdown-item")
        oLink:SetAttr("href",aItems[i][2])
        oLink:SetContent(aItems[i][1])
        oItem:AddChild(oLink)
        oMenu:AddChild(oItem)
    next i

    oWrapper:=WebExControl():New("li")
    oWrapper:SetAttr("class","nav-item dropdown")
    oWrapper:AddChild(oToggle)
    oWrapper:AddChild(oMenu)

    ::jDropDowns[oWrapper:cID]:=oWrapper

return(oWrapper)

method RenderHTML() class WebExNavTop

    local cHTML as character

    local oUl as object
    local oIcon as object
    local oBrand as object
    local oCollapse as object
    local oContainer as object
    local oSidebarToggle as object

    // Botao para abrir/recolher o menu lateral (sidebar)
    oSidebarToggle:=WebExControl():New("button")
    oSidebarToggle:SetAttr("class","navbar-toggler border-0 bg-transparent mb-1 ps-0 pe-0 ms-0 me-0")
    oSidebarToggle:SetAttr("type","button")
    oSidebarToggle:SetAttr("data-bs-toggle","collapse")
    oSidebarToggle:SetAttr("data-bs-target","#webex-sidebar")
    oSidebarToggle:SetAttr("aria-controls","webex-sidebar")
    oSidebarToggle:SetAttr("aria-expanded","false")
    oSidebarToggle:SetAttr("aria-label","Alternar menu")
    oSidebarToggle:SetAttr("style","margin-bottom: 2px; outline:none; box-shadow:none;")

    oIcon:=WebExIcon():New("bi-list")
    oIcon:AddClass("fs-4 ms-0 me-0")
    oSidebarToggle:AddChild(oIcon)

    // Marca do sistema
    oBrand:=WebExControl():New("a")
    oBrand:SetAttr("class","navbar-brand ps-0 pe-0 ms-0 me-0")
    oBrand:SetAttr("href","#")
    oBrand:SetContent(::cBrand)

    // Container principal do topo
    oContainer:=WebExControl():New("div")
    oContainer:SetAttr("class","container-fluid d-flex flex-column align-items-start px-0")

    oContainer:AddChild(oSidebarToggle)
    oContainer:AddChild(oBrand)

    if ((Len(::jLinks:GetNames())>0).or.(Len(::jDropDowns:GetNames())>0))
        oUl:=WebExControl():New("ul")
        oUl:SetAttr("class","navbar-nav me-auto mb-0")
        // Adiciona os links
        aEval(::jLinks:GetNames(), {|k| oUl:AddChild(::jLinks[k]) })
        // Adiciona os dropdowns
        aEval(::jDropDowns:GetNames(), {|k| oUl:AddChild(::jDropDowns[k]) })
        // Menu colapsavel da barra superior
        oCollapse:=WebExControl():New("div")
        oCollapse:SetAttr("class","collapse navbar-collapse p-0 m-0")
        oCollapse:SetAttr("id",::cID+"_menu")
        oCollapse:AddChild(oUl)
        oContainer:AddChild(oCollapse)
    endif

    ::AddChild(oContainer)

    cHTML:=_Super:RenderHTML()

return(cHTML)
