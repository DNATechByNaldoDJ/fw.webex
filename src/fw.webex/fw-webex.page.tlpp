/*
  __                            _
 / _|__      ____      __  ___ | |__    ___ __  __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\

Released to Public Domain.
--------------------------------------------------------------------------------------
*/

namespace FWWebEx

#include "fw.webex.th"

#include "totvs.ch"
#include "parmtype.ch"
#include "tlpp-core.th"

class WebExPage from WebExControl

    public data cTitle as character

    public method New(cTitle as character) as object

    public method Render() as character

endclass

method New(cTitle) class WebExPage
   paramtype 1 var cTitle as character
   _Super:New()
   ::cTitle:=cTitle
return(self)

method Render() class WebExPage

    local cHTML:="<!DOCTYPE html>" as character
    local cBody:=::ToHtml() as character

    local oObj as object

    WITH WEBEXOBJECT oObj CLASS WebExControl TYPE html
        .:SetAttr("lang","pt-br")
        WITH WEBEXOBJECT CLASS WebExControl TYPE head
            WITH WEBEXOBJECT CLASS WebExControl TYPE meta
                .:SetAttr("charset","UTF-8")
            END WEBEXOBJECT
            WITH WEBEXOBJECT CLASS WebExControl TYPE meta
                .:SetAttr("application-name","FWWebEx")
            END WEBEXOBJECT
            WITH WEBEXOBJECT CLASS WebExControl TYPE meta
                .:SetAttr("name","viewport")
                .:SetAttr("namcontente","width=device-width, initial-scale=1")
            END WEBEXOBJECT
            WITH WEBEXOBJECT CLASS WebExControl TYPE !--
                .:SetContent("PO UI CSS")
            END WEBEXOBJECT
            WITH WEBEXOBJECT CLASS WebExControl TYPE link
                .:SetAttr("href","https://cdn.jsdelivr.net/npm/@po-ui/style/css/po-theme-default.min.css")
                .:SetAttr("rel","stylesheet")
            END WEBEXOBJECT
            WITH WEBEXOBJECT CLASS WebExControl TYPE !--
                .:SetContent("Bootstrap CSS")
            END WEBEXOBJECT
            WITH WEBEXOBJECT CLASS WebExControl TYPE link
                .:SetAttr("href","https://cdn.jsdelivr.net/npm/bootstrap@5.3.3/dist/css/bootstrap.min.css")
                .:SetAttr("rel","stylesheet")
            END WEBEXOBJECT
            WITH WEBEXOBJECT CLASS WebExControl TYPE script
                .:SetAttr("src","https://cdn.jsdelivr.net/npm/bootstrap@5.3.3/dist/js/bootstrap.bundle.min.js")
            END WEBEXOBJECT
            WITH WEBEXOBJECT CLASS WebExControl TYPE title
                .:SetContent(::cTitle)
            END WEBEXOBJECT
        END WEBEXOBJECT
        WITH WEBEXOBJECT CLASS WebExControl TYPE body
            .:SetAttr("class","p-4 bg-dark text-light")
            .:SetContent(cBody)
        END WEBEXOBJECT
        cHTML+=oObj:ToHTML()
    END WEBEXOBJECT

    FreeObj(@oObj)

return(cHTML)
