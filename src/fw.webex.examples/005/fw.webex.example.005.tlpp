/*
  __                            _
 / _|__      ____      __  ___ | |__    ___ __  __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\

Released to Public Domain.
--------------------------------------------------------------------------------------
*/

#include "fw.webex.th"

#include "shell.ch"
#include "totvs.ch"

using namespace FWWebEx

procedure u_FWWebExExample_005()

    local lMainWnd:=(Type("oMainWnd")=="O") as logical

    private lRedefineBottom as logical

    if (!lMainWnd)
        private oMainWnd as object
        lRedefineBottom:=.T.
        DEFINE WINDOW oMainWnd FROM 00,00 TO 1024,768 TITLE ProcName()
        ACTIVATE WINDOW oMainWnd MAXIMIZED ON INIT (FWWebExExample_005(),oMainWnd:End())
        FreeObj(@oMainWnd)
    else
        lRedefineBottom:=.F.
        FWWebExExample_005()
    endif

return

static procedure FWWebExExample_005()

    local cHTML as character
    local cHTMLFile as character

    local cScript as character
    local cProcName:=ProcName() as character

    local oDiv as object
    local oButton as object

    local oPage:=WebExPage():New("Exemplo 005 - Formul&aacute;rio ViaCEP") as object
    local oForm:=WebExForm():New("Consulta de CEP") as object
    local oScript:=WebExControl():New("script") as object

    oForm:SetMethod("get")
    oForm:SetAction("#")

    // Campo de busca
    oForm:AddField("CEP","cep","text","Digite o CEP")

    // Campos que serao atualizados via Ajax (ja definidos)
    oForm:AddField("Logradouro","logradouro","text","")
    oForm:AddField("Bairro","bairro","text","")
    oForm:AddField("Cidade","localidade","text","")
    oForm:AddField("UF","uf","text","")
    oForm:AddField("C&oacute;digo IBGE","ibge","text","")

    oDiv:=WebExControl():New("div")
    oDiv:SetAttr("id","resultadoCEP")

    oButton:=WebExButton():New("Buscar")
    oButton:SetAttr("onclick","buscarCEP(); return false;")

    oForm:AddChild(oDiv)
    oForm:AddChild(oButton)

    oPage:AddChild(oForm)

    // Adiciona o script para consulta Ajax do ViaCEP
    beginContent var cScript
        function buscarCEP() {
            const cep = document.getElementsByName('cep')[0].value.replace(/[^0-9]/g,'');
            if (cep.length !== 8) {
                document.getElementById('resultadoCEP').innerHTML = '<div class=\"alert alert-danger\">CEP inv&aacute;lido.</div>';
                return;
            }
            fetch('https://viacep.com.br/ws/' + cep + '/json/')
                .then(response => response.json())
                .then(data => {
                if (data.erro) {
                    document.getElementById('resultadoCEP').innerHTML = '<div class=\"alert alert-danger\">CEP n&atilde;o encontrado.</div>';
                } else {
                    document.getElementsByName('logradouro')[0].value = data.logradouro || '';
                    document.getElementsByName('bairro')[0].value = data.bairro || '';
                    document.getElementsByName('localidade')[0].value = data.localidade || '';
                    document.getElementsByName('uf')[0].value = data.uf || '';
                    document.getElementsByName('ibge')[0].value = data.ibge || '';
                    document.getElementById('resultadoCEP').innerHTML = '';
                }
            }).catch(() => {
                document.getElementById('resultadoCEP').innerHTML = '<div class=\"alert alert-danger\">Erro ao consultar o CEP.</div>';
            });
        }
    endContent

    oScript:SetContent(cScript)

    oPage:AddChild(oScript)
    cHTML:=oPage:Render()

    FreeObj(@oDiv)
    FreeObj(@oPage)
    FreeObj(@oForm)
    FreeObj(@oScript)
    FreeObj(@oButton)

    cHTML:=EncodeUTF8(cHTML)
    if (!lIsDir("\web\tmp\"))
        FWMakeDir("\web\tmp\",.F.)
    endif
    cHTMLFile:="\web\tmp\"+Lower(cProcName)+".html"
    MemoWrite(cHTMLFile,cHTML)

    htmlFileShow(cHTML,cProcName,cHTMLFile)

    fErase(cHTMLFile)

return

static procedure htmlFileShow(cHTML as character,cTitle as character,cHTMLFile as character)

    local aButtons as array

    local bErrorBlock as codeblock

    local cHTMLFileTmpFile as character
    local cHTMLFileServer as character
    local cHTMLFileNavigate as character

    local cSPPDrive as character
    local cSPPPath as character
    local cSPPFile as character
    local cSPPExt as character

    local jButtons as json

    local lAbrir as logical
    local lIsDark:=WebApp():ThemeIsDark() as logical
    local lHasTheme as logical

    local nTop as numeric
    local nLeft as numeric
    local nBottom as numeric
    local nRight as numeric

    local oDlg as object
    local oGrp as object

    local oError as object
    local oTheme as object

    local oTWebEngine as object
    local oTWebChannel as object

    local oFWDefSize as object

    begin sequence

        jButtons:=JSONObject():New()

        aButtons:=array(0)
        aAdd(aButtons,{"&Sair",{||oDlg:End()},OemToAnsi("Sair"),OemtoAnsi("Sair")})
        aAdd(aButtons,{"&Print",{||oTWebEngine:Print()},OemToAnsi("Print"),OemtoAnsi("Print")})
        aAdd(aButtons,{"Print PD&F",{||oTWebEngine:PrintPDF()},OemToAnsi("Print PDF"),OemtoAnsi("Print PDF")})

        lAbrir:=file(cHTMLFile)

        if (lAbrir)
            if (WebApp():WebAgentIsConnected())
                bErrorBlock:=ErrorBlock({|oError|Break(oError)})
                begin sequence
                    //CpyF2Web(<cOrigem>,[lIsUserDiskDir],[lCompactCopy],[lChangeCase],[lUnZipFile])
                    cHTMLFileNavigate:=CpyF2Web(cHTMLFile,.T.,.T.,.F.,.F.)
                recover
                    begin sequence
                        //CpyF2Web(<cOrigem>,[lIsUserDiskDir],[lCompactCopy],[lChangeCase],[lUnZipFile])
                        cHTMLFileNavigate:=CpyF2Web(cHTMLFile,.F.,.T.,.F.,.F.)
                    end sequence
                end sequence
                ErrorBlock(bErrorBlock)
                if ((Empty(cHTMLFileNavigate)).and.(lIsDir("\web\tmp\")))
                    splitPath(cHTMLFile,@cSPPDrive,@cSPPPath,@cSPPFile,@cSPPExt)
                    cHTMLFileServer:="\web\tmp\"
                    cHTMLFileServer+=cSPPFile
                    cHTMLFileServer+=cSPPExt
                    cHTMLFileNavigate:=cHTMLFileServer
                    bErrorBlock:=ErrorBlock({|oError|Break(oError)})
                    begin sequence
                        //CpyF2Web(<cOrigem>,[lIsUserDiskDir],[lCompactCopy],[lChangeCase],[lUnZipFile])
                        cHTMLFileNavigate:=CpyF2Web(cHTMLFileNavigate,.T.,.T.,.F.,.F.)
                    recover
                        begin sequence
                            //CpyF2Web(<cOrigem>,[lIsUserDiskDir],[lCompactCopy],[lChangeCase],[lUnZipFile])
                            cHTMLFileNavigate:=CpyF2Web(cHTMLFileNavigate,.F.,.T.,.F.,.F.)
                        recover
                            cHTMLFileNavigate:=cHTMLFileServer
                        end sequence
                    end sequence
                    ErrorBlock(bErrorBlock)
                endif
                splitPath(cHTMLFile,@cSPPDrive,@cSPPPath,@cSPPFile,@cSPPExt)
                cHTMLFileTmpFile:=GetTempPath()
                cHTMLFileTmpFile+=cSPPFile
                cHTMLFileTmpFile+=cSPPExt
                __CopyFile(cHTMLFile,cHTMLFileTmpFile)
                aAdd(aButtons,{"&Abrir",{||ShellExecute("open",cHTMLFileTmpFile,"","",SW_SHOWMAXIMIZED)},OemToAnsi("Abir"),OemtoAnsi("Abir")})
            endif
        endif

        oFWDefSize:=FWDefSize():New(.F.,.F.)
        oFWDefSize:lLateral:=.F.
        oFWDefSize:AddObject("TWebEngine",100,95,.T.,.T.)
        oFWDefSize:AddObject("ButtonActions",100,05,.F.,.F.)

        oFWDefSize:Process()

        if (oFWDefSize:aWindSize[3]==0)
            nTop:=0
            nLeft:=0
            nBottom:=530
            nRight:=1330
        else
            nTop:=oFWDefSize:aWindSize[1]
            nLeft:=oFWDefSize:aWindSize[2]
            nBottom:=oFWDefSize:aWindSize[3]
            nRight:=oFWDefSize:aWindSize[4]
        endif

        if (lRedefineBottom)
            nBottom-=50
        endif

        DEFINE MSDIALOG oDlg TITLE cTitle FROM nTop,nLeft TO nBottom,nRight OF GetWndDefault() STATUS PIXEL

            if (oFWDefSize:aWindSize[3]>0)
                nTop:=oFWDefSize:GetDimension("TWebEngine","LININI")
                nLeft:=oFWDefSize:GetDimension("TWebEngine","COLINI")
                nBottom:=oFWDefSize:GetDimension("TWebEngine","LINEND")
                nRight:=oFWDefSize:GetDimension("TWebEngine","COLEND")
                nTop-=5
                nBottom+=1
                nRight-=5
                if (lRedefineBottom)
                    nBottom-=25
                endif
            endif

            oTWebChannel:=TWebChannel():New()
            oTWebChannel:Connect()

            oTWebEngine:=TWebEngine():New(oDlg,nTop,nLeft,nRight,nBottom,/*cURL*/,oTWebChannel:nPort)
            oTWebEngine:Align:=CONTROL_ALIGN_ALLCLIENT

            nTop:=oFWDefSize:GetDimension("ButtonActions","LININI")
            nBottom:=oFWDefSize:GetDimension("ButtonActions","LINEND")

            if (oFWDefSize:aWindSize[3]>0)
                if (lRedefineBottom)
                    nTop-=25
                    nBottom-=25
                endif
                nTop-=3
                nRight+=3
            endif

            if (lIsDark)
                @ nTop,03 GROUP oGrp TO nBottom+2,nRight PROMPT "" OF oDlg PIXEL
            else
                @ nTop,03 GROUP oGrp TO nBottom+2,nRight PROMPT "" OF oDlg COLOR 0,16777215 PIXEL
            endif

            nTop+=1
            nRight+=5

            try
                oTheme:=totvs.framework.css.ProtheusTheme():New()
                lHasTheme:=.T.
            catch oError
                lHasTheme:=.F.
                FreeObj(@oError)
            endtry

            //Botoes (da direita pra a esquerda)
            @ nTop,(nRight)-((50*1)+06) BUTTON jButtons["1"] PROMPT aButtons[1][1] SIZE 50,08 OF oGrp ACTION Eval(aButtons[1][2]) PIXEL
            if (lHasTheme)
                jButtons["1"]:SetCSS(oTheme:getPrimaryButtonCSS())
            endif
            @ nTop,(nRight)-((50*2)+09) BUTTON jButtons["2"] PROMPT aButtons[2][1] SIZE 50,08 OF oGrp ACTION Eval(aButtons[2][2]) PIXEL
            if (lHasTheme)
                jButtons["2"]:SetCSS(oTheme:getPrimaryButtonCSS())
            endif
            @ nTop,(nRight)-((50*3)+12) BUTTON jButtons["3"] PROMPT aButtons[3][1] SIZE 50,08 OF oGrp ACTION Eval(aButtons[3][2]) PIXEL
            if (lHasTheme)
                jButtons["3"]:SetCSS(oTheme:getPrimaryButtonCSS())
            endif
            if (lAbrir)
                @ nTop,(nRight)-((50*4)+15) BUTTON jButtons["4"] PROMPT aButtons[4][1] SIZE 50,08 OF oGrp ACTION Eval(aButtons[4][2]) PIXEL
                if (lHasTheme)
                    jButtons["4"]:SetCSS(oTheme:getPrimaryButtonCSS())
                endif
            endif

            oDlg:lEscClose:=.F.

            oTWebEngine:Navigate(cHTMLFileNavigate)

            oDlg:lCentered:=(!lRedefineBottom)

        ACTIVATE MSDIALOG oDlg

        if (valType(oTWebChannel)=="O")
            oTWebChannel:Disconnect()
        endif

        if ((!Empty(cHTMLFile)).and.(File(cHTMLFile)))
            fErase(cHTMLFile)
        endif

        if ((!Empty(cHTMLFileNavigate)).and.(File(cHTMLFileNavigate)))
            fErase(cHTMLFileNavigate)
        endif

        if ((!Empty(cHTMLFileServer)).and.(File(cHTMLFileServer)))
            fErase(cHTMLFileServer)
        endif

        if ((!Empty(cHTMLFileTmpFile)).and.(File(cHTMLFileTmpFile)))
            fErase(cHTMLFileTmpFile)
        endif

    end sequence

    FWFreeArray(@aButtons)

    FreeObj(@oGrp)
    FreeObj(@oDlg)
    FreeObj(@oTheme)
    FreeObj(@jButtons)
    FreeObj(@oFWDefSize)

    FreeObj(@oTWebEngine)
    FreeObj(@oTWebChannel)

    DelClassIntF()

return
