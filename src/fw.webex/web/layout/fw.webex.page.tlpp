/*
  __                            _
 / _|__      ____      __  ___ | |__    ___ __  __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\

+-----> WebExPage
        +
        |
        +-----> WebExSideBar (com WebExNavSide ou WebExNavTop)
        |
        +-----> WebExMain
                +
                |
                +-----> WebExContainer
                        +
                        |
                        +-----> Conteudo

Released to Public Domain.
--------------------------------------------------------------------------------------
*/

namespace FWWebEx

#include "fw.webex.th"

class WebExPage from WebExControl

    public data cTitle as character

    public data jStyle as json

    public data oBody as object

    public method New(cTitle as character) as object
    public method AddChild(oChild as object) as object
    public method AddStyle(cStyle) as object
    public method SetBody(oBody as object) as object
    public method RenderHTML() as character

endclass

method New(cTitle) class WebExPage
    paramtype cTitle as character optional default "FWWEBEX"
    _Super:New("div")
    ::AddClass("main-div")
    ::cTitle:=cTitle
    ::jStyle:=JSONObject():New()
return(self)

method AddChild(oChild) class WebExPage
    local cClassName as character
    paramtype 1 var oChild as object
    cClassName:=GetClassName(oChild)
    if (!Left(cClassName,8)=="FWWEBEX.")
        CLASSPARAMEXCEPTION PARAM 1 VAR oChild TEXT cClassName MESSAGE "Object must belong to the FWWEBEX namespace. Got '"+cClassName+"' instead. Check your class declaration."
    elseif ((!MethIsMemberOf(oChild,"ISKINDOF",.T.)).or.(!oChild:IsKindOf("WEBEXCONTROL",.T.)))
        CLASSPARAMEXCEPTION PARAM 1 VAR oChild TEXT cClassName MESSAGE "Object must inherit from FWWEBEX.WebExControl. Received: "+cClassName+"."
    elseif (oChild:IsKindOf("WEBEXPAGE",.T.))
        CLASSPARAMEXCEPTION PARAM 1 VAR oChild TEXT cClassName MESSAGE "Cannot nest a WebExPage inside another WebExPage. Received: "+cClassName+". A WebExPage must be the top-level container."
    endif
    if (oChild:IsKindOf("WEBEXBODY"))
        ::SetBody(oChild)
    else
        _Super:AddChild(oChild)
    endif
return(self)

method SetBody(oBody) class WebExPage
    local cClassName as character
    paramtype 1 var oBody as object
    cClassName:=GetClassName(oBody)
    if (!Left(cClassName,8)=="FWWEBEX.")
        CLASSPARAMEXCEPTION PARAM 1 VAR oBody TEXT cClassName MESSAGE "Object must belong to the FWWEBEX namespace. Got '"+cClassName+"' instead. Check your class declaration."
    elseif ((!MethIsMemberOf(oBody,"ISKINDOF",.T.)).or.(!oBody:IsKindOf("WEBEXCONTROL",.T.)))
        CLASSPARAMEXCEPTION PARAM 1 VAR oBody TEXT cClassName MESSAGE "Object must inherit from FWWEBEX.WebExControl. Received: "+cClassName+"."
    endif
    if (!oBody:IsKindOf("WEBEXBODY",.T.))
        ::oBody:=WebExBody():New()
        ::oBody:AddChild(oBody)
    else
        ::oBody:=oBody
    endif
return(self)

method AddStyle(cStyle) class WebExPage
    local oStyle as object
    paramtype 1 var cStyle as character
    WITH WEBEXOBJECT oStyle CLASS WebExControl TYPE style
        .:SetContent(cStyle)
        ::jStyle[oStyle:cID]:=oStyle
    END WEBEXOBJECT
return(self)

method RenderHTML() class WebExPage

    local cHTML as character
    local cTheme as character
    local cStyle as character
    local cScript as character

    local jHeader as json
    local jAssets as json
    local jHeadScripts as json
    local jPageScripts as json

    local lIsDark as logical

    local oPage as object
    local oBody as object
    local oMain as object
    local oHeader as object

    cHTML:="<!DOCTYPE html>"

    lIsDark:=WebApp():ThemeIsDark(@cTheme)

    if ((!lIsDark).and.(!Empty(cTheme)).and.(cTheme=="SUNSET"))

        beginContent var cStyle

            :root {
                --totvs-red: #c62828;
                --totvs-light-gray: #f4f4f4;
                --totvs-dark-gray: #dcdcdc;
                --totvs-border: #ccc;
                --totvs-text: #000;
                --totvs-highlight: #e0e0e0;
                --totvs-btn-bg: #ffffff;
            }

            body {
                background-color: var(--totvs-light-gray) !important;
                color: var(--totvs-text) !important;
                font-family: "Segoe UI", sans-serif !important;
            }

            .btn {
                background-color: var(--totvs-btn-bg) !important;
                color: var(--totvs-red) !important;
                border: 1px solid var(--totvs-red) !important;
                border-radius: 4px !important;
                font-weight: bold;
                padding: 6px 12px;
                transition: all 0.2s ease-in-out !important;
            }

            .btn:hover,
            .btn:focus {
                background-color: var(--totvs-red) !important;
                color: #fff !important;
            }

            .form-control,
            .form-select {
                background-color: #fff !important;
                color: var(--totvs-text) !important;
                border: 1px solid var(--totvs-border) !important;
                border-radius: 3px !important;
                padding: 5px 10px;
            }

            .form-control:focus,
            .form-select:focus {
                border-color: var(--totvs-red) !important;
                box-shadow: 0 0 3px rgba(198, 40, 40, 0.4) !important;
            }

            .card {
                border: 1px solid var(--totvs-border) !important;
                border-radius: 6px;
                padding: 15px;
                color: var(--totvs-text);
                box-shadow: 0 0 10px rgba(0,0,0,0.05);
            }

            .navbar {
                background-color: #fff !important;
                border-bottom: 2px solid var(--totvs-red) !important;
            }

            .navbar .nav-link,
            .navbar-brand {
                color: var(--totvs-text) !important;
            }

            .navbar .nav-link:hover {
                color: var(--totvs-red) !important;
            }

            .table {
                background-color: #fff !important;
                color: var(--totvs-text) !important;
                border: 1px solid var(--totvs-border);
            }

            .table thead {
                background-color: var(--totvs-dark-gray) !important;
                color: var(--totvs-text) !important;
            }

            .table-bordered,
            .table-bordered th,
            .table-bordered td {
                border: 1px solid var(--totvs-border) !important;
            }

            .table tbody tr:hover {
                background-color: var(--totvs-highlight) !important;
            }

            a {
                color: var(--totvs-red) !important;
                text-decoration: none;
            }

            a:hover {
                text-decoration: underline;
            }

            input[type="checkbox"],
            input[type="radio"] {
                accent-color: var(--totvs-red) !important;
            }

        endContent

        ::AddStyle(cStyle)

    endif

    if (Empty(::oBody))
        oMain:=WebExMain():New()
        oBody:=WebExBody():New()
        oBody:AddChild(oMain)
    else
        lHasMain:=::oBody:IsKindOf("WEBEXMAIN")
        if (lHasMain)
            oBody:=::oBody
        else
            oMain:=WebExMain():New()
            oMain:AddChild(::oBody)
            oBody:=WebExBody():New()
            oBody:AddChild(oMain)
        endif
    endif
    ::oBody:=oBody
    WITH WEBEXOBJECT ::oBody
        WITH WEBEXOBJECT CLASS WebExFooter
        END WEBEXOBJECT
    END WEBEXOBJECT
    ::oBody:RenderHTML()
    ::oBody:SetRenderChildren(.F.)

    WITH WEBEXOBJECT oPage CLASS WebExHtml
        .:SetAttr("lang","pt-br")
        if (lIsDark)
            .:SetAttr("data-bs-theme","dark")
        endif
        WITH WEBEXOBJECT oHeader CLASS WebExHead
            ***************************************************************************************************************************
            //Title
            WITH WEBEXOBJECT CLASS WebExTitle ARGS ::cTitle
            END WEBEXOBJECT
            ***************************************************************************************************************************
            //Meta
            WITH WEBEXOBJECT CLASS WebExMeta
                .:SetAttr("charset","UTF-8")
            END WEBEXOBJECT
            WITH WEBEXOBJECT CLASS WebExMeta
                .:SetAttr("application-name","FWWebEx")
            END WEBEXOBJECT
            WITH WEBEXOBJECT CLASS WebExMeta
                .:SetAttr("name","viewport")
                .:SetAttr("content","width=device-width, initial-scale=1")
            END WEBEXOBJECT
            ***************************************************************************************************************************
            //PO UI CSS
                WITH WEBEXOBJECT CLASS WebExComment
                    .:SetContent("PO UI CSS")
                END WEBEXOBJECT
                WITH WEBEXOBJECT CLASS WebExLink
                    .:SetAttr("href","https://cdn.jsdelivr.net/npm/@po-ui/style/css/po-theme-default.min.css")
                    .:SetAttr("rel","stylesheet")
                END WEBEXOBJECT
            //PO UI CSS
            ***************************************************************************************************************************
            //Bootstrap CSS
                WITH WEBEXOBJECT CLASS WebExComment
                    .:SetContent("Bootstrap CSS")
                END WEBEXOBJECT
                WITH WEBEXOBJECT CLASS WebExLink
                    .:SetAttr("href","https://cdn.jsdelivr.net/npm/bootstrap/dist/css/bootstrap.min.css")
                    .:SetAttr("rel","stylesheet")
                END WEBEXOBJECT
            //Bootstrap CSS
            ***************************************************************************************************************************
            //Bootstrap Optional JavaScript
                WITH WEBEXOBJECT CLASS WebExComment
                    .:SetContent("Optional JavaScript")
                END WEBEXOBJECT
                WITH WEBEXOBJECT CLASS WebExComment
                    .:SetContent("jQuery first, then Popper.js, then Bootstrap JS")
                END WEBEXOBJECT
                WITH WEBEXOBJECT CLASS WebExScript ARGS .F.
                    .:SetContent("https://releases.jquery.com/git/jquery-git.slim.min.js")
                END WEBEXOBJECT
                WITH WEBEXOBJECT CLASS WebExScript ARGS .F.
                    .:SetContent("https://releases.jquery.com/git/jquery-git.min.js")
                END WEBEXOBJECT
                WITH WEBEXOBJECT CLASS WebExScript ARGS .F.
                    .:SetContent("https://cdn.jsdelivr.net/npm/@popperjs/core/dist/umd/popper.min.js")
                END WEBEXOBJECT
                WITH WEBEXOBJECT CLASS WebExScript ARGS .F.
                    .:SetContent("https://cdn.jsdelivr.net/npm/bootstrap/dist/js/bootstrap.min.js")
                END WEBEXOBJECT
            //Bootstrap Optional JavaScript
            ***************************************************************************************************************************
            //Custom Header
            jHeader:=oHeader:GetHeader()
            aEval(jHeader:GetNames(),{|cName|oHeader:AddChild(jHeader[cName])})
            ***************************************************************************************************************************
            //Styles
            aEval(::jStyle:GetNames(),{|cName|oHeader:AddChild(::jStyle[cName])})
            ***************************************************************************************************************************
            //Scripts Head
            jHeadScripts:=oHeader:GetHeadScripts()
            aEval(jHeadScripts:GetNames(),{|cName|oHeader:AddChild(jHeadScripts[cName])})
            ***************************************************************************************************************************
        END WEBEXOBJECT
        ***************************************************************************************************************************
        WITH WEBEXOBJECT ::oBody
        END WEBEXOBJECT
        ***************************************************************************************************************************
        //Scripts
        jPageScripts:=oPage:GetPageScriptBundle()
        aEval(jPageScripts:GetNames(),{|cName|oPage:AddChild(jPageScripts[cName])})
        if ((::ExternalAssetsIsEnabled()).and.(::lExternalAssets))
            //External Assets (Do Not Use WebExScript Here)
            WITH WEBEXOBJECT CLASS WebExControl TYPE script
                .:SetContent("/*__EXTERNAL_ASSETS__*/")
            END WEBEXOBJECT
        endif
    END WEBEXOBJECT

    cHTML+=oPage:RenderHTML()

    if ((::ExternalAssetsIsEnabled()).and.(::lExternalAssets))
        jAssets:=::GetAssets()
        if (Len(jAssets:GetNames())>0)
            beginContent var cScript
                (
                    function(){
                        var useLocal = (
                            location.protocol === "file:"
                        );
                        var assets = __jAssets__;
                        Object.keys(assets).forEach(function(id){
                            var asset = assets[id];
                            var el;
                            if (asset.type === "style") {
                                el = document.createElement("link");
                                el.rel = "stylesheet";
                                el.href = useLocal ? asset.local : asset.remote;
                            } else if (asset.type === "script") {
                                el = document.createElement("script");
                                el.src = useLocal ? asset.local : asset.remote;
                                el.type = "text/javascript";
                            }
                            if (el) {
                                el.setAttribute("data-id", asset.id); // opcional, pra rastrear depois
                                document.head.appendChild(el);
                            }
                        });
                    }
                )();
            endContent
            cScript:=StrTran(cScript,"__jAssets__",jAssets:ToJSON())
            cHTML:=StrTran(cHTML,"/*__EXTERNAL_ASSETS__*/",cScript)
        endif
    endif

    FreeObj(@jHeader)
    FreeObj(@jHeadScripts)
    FreeObj(@jPageScripts)

return(cHTML)
