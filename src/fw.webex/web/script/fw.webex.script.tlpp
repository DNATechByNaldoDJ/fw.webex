/*
  __                            _
 / _|__      ____      __  ___ | |__    ___ __  __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\

Released to Public Domain.
--------------------------------------------------------------------------------------
*/

namespace FWWebEx

#include "fw.webex.th"

class WebExScript from WebExControl
    public data cScriptSRC as character
    public data lScriptUseAddChild as logical
    public data lScriptInPage as logical
    public method New(lScriptInPage as logical,lScriptUseAddChild as logical) as object
    public method SetContent(cScriptSRC as character,lScriptAddScript as logical) as object
endclass

method New(lScriptInPage,lScriptUseAddChild) class WebExScript
    paramtype 1 var lScriptInPage as logical optional default .T.
    paramtype 2 var lScriptUseAddChild as logical optional default (!lScriptInPage)
    _Super:New("script")
    ::lScriptInPage:=lScriptInPage
    ::lScriptUseAddChild:=lScriptUseAddChild
return(self)

method SetContent(cScriptSRC,lScriptAddScript) class WebExScript
    paramtype 1 var cScriptSRC as character
    paramtype 2 var lScriptAddScript as logical optional default .T.
    ::cScriptSRC:=cScriptSRC
    if (!::lScriptInPage)
        ::SetAttr("src",::cScriptSRC)
        ::SetAttr("defer")
    else
        _Super:SetContent(::cScriptSRC)
    endif
    if ((lScriptAddScript).and.(!::lScriptUseAddChild))
        ::AddScript(self)
    endif
return(self)
