/*
  __                            _
 / _|__      ____      __  ___ | |__    ___ __  __
| |_ \ \ /\ / /\ \ /\ / / / _ \| '_ \  / _ \\ \/ /
|  _| \ V  V /  \ V  V / |  __/| |_) ||  __/ >  <
|_|    \_/\_/    \_/\_/   \___||_.__/  \___|/_/\_\

Released to Public Domain.
--------------------------------------------------------------------------------------
*/

namespace FWWebEx

#include "fw.webex.th"

class WebExForm from WebExControl

    public data cFormTitle as character
    public data cFormMethod as character
    public data cFormAction as character

    public data jFormFields as json
    public data jFormButtons as json

    public data lFormButtonsFirst as logical

    public method New(cFormTitle as character,lFormButtonsFirst as logical) as object
    public method SetMethod(cFormMethod as character) as object
    public method SetAction(cFormAction as character) as object
    public method AddField(cLabel as character,cName as character,cType as character,cPlaceholder as character) as object
    public method AddButton(oButton as object) as object

    public method RenderHTML() as character

endclass

method New(cFormTitle,lFormButtonsFirst) class WebExForm
    paramtype 1 var cFormTitle as character
    paramtype 2 var lFormButtonsFirst as logical optional default .F.
    ::cFormTitle:=cFormTitle
    ::lFormButtonsFirst:=lFormButtonsFirst
    _Super:New("form")
    ::SetMethod("get")
    ::SetAction("#")
    ::jFormFields:=JSONObject():New()
    ::jFormButtons:=JSONObject():New()
return(self)

method SetMethod(cFormMethod) class WebExForm
   paramtype 1 var cFormMethod as character
   ::cFormMethod:=Lower(AllTrim(cFormMethod))
return(self)

method SetAction(cFormAction) class WebExForm
   paramtype 1 var cFormAction as character
   ::cFormAction:=AllTrim(cFormAction)
return(self)

method AddField(cLabel,cName,cType,cPlaceholder) class WebExForm
   local oField as object
   paramtype 1 var cLabel as character
   paramtype 2 var cName as character
   paramtype 3 var cType as character
   paramtype 4 var cPlaceholder as character
   oField:=WebExField():New(cLabel,cName,cType,cPlaceholder)
   ::jFormFields[oField:GetID()]:=oField
return(self)

method AddButton(oButton) class WebExForm
   paramtype 1 var oButton as object class WEBEXBUTTON
   ::jFormButtons[oButton:GetID()]:=oButton
return(self)

method RenderHTML() class WebExForm

    local aNames as array

    local nName as numeric
    local nNames as numeric

    local jChilds as json

    local oChild as object
    local oFieldSet as object

    ::SetAttr("method",::cFormMethod)
    ::SetAttr("action",::cFormAction)
    ::SetAttr("class","p-3 border rounded")

    oFieldSet:=WebExFieldSet():New(::cFormTitle)

    jChilds:=if(::lFormButtonsFirst,::jFormButtons,::jFormFields)

    aNames:=jChilds:GetNames()
    nNames:=Len(aNames)
    for nName:=1 to nNames
        cName:=aNames[nName]
        oChild:=jChilds[cName]
        oFieldSet:AddChild(oChild)
    next nName

    FWFreeArray(@aNames)

    jChilds:=if(::lFormButtonsFirst,::jFormFields,::jFormButtons)
    aNames:=jChilds:GetNames()
    nNames:=Len(aNames)
    for nName:=1 to nNames
        cName:=aNames[nName]
        oChild:=jChilds[cName]
        oFieldSet:AddChild(oChild)
    next nName

    ::AddChild(oFieldSet)

return(_Super:RenderHTML())
